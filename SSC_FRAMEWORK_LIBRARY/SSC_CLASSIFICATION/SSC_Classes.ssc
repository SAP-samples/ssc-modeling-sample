/**
 * @author SAP
 *
 */
class SSC_INST {
	name "Instance"
	characteristics
	 SSC_INSTANCE_NUMBER invisible,
	 SSC_DISPLAY_NAME    invisible
}
class SSC_MATERIAL {
	name "Material"
	characteristics
	SSC_MATERIAL_ID invisible,
	SSC_ITEM_QUANTITY_ACTUAL invisible
}
class SSC_PARENT  extends SSC_INST, SSC_MATERIAL {
	name "SSC SD Parent Item"
	characteristics
	HAS_PART_SD_SOFT invisible,
	HAS_PART_SD_HARD invisible,
	HAS_PART_PP invisible,
	BOM_POSITION_DETAILS_MV invisible,
	BOM_FIXED_QTY_DETAILS_MV invisible
}
class SSC_CHILD extends SSC_INST, SSC_MATERIAL {
	name "SSC Child Item"
	characteristics
	IS_PART_OF_SD_SOFT invisible,
	IS_PART_OF_SD_HARD invisible,
	IS_PART_OF_PP invisible,
	BOM_POSITION_NUMBER invisible,
	BOM_POSITION_CLASS_TYPE invisible,
	BOM_POSITION_OBJECT_KEY invisible,
	BOM_POSITION_OBJECT_TYPE invisible,
	BOM_POSITION_QUANTITY invisible,
	BOM_POSITION_QUANTITY_ORDERED invisible,
	BOM_POSITION_QUANTITY_DISPLAY,
	BOM_POSITION_QUANTITY_NUMCHAR invisible,
	BOM_POSITION_QUANTITY_IS_FIXED invisible defaultValues ['NO'],
	BOM_FIXED_QTY_DETAILS_MV invisible
}
class SSC_KB_ROOT {
	name "KB Root item"
	characteristics
	ECC_KB_NAME invisible,
	ECC_KB_VERSION invisible,
	NON_CONFIGURABLE_TYPES_IN_ECC invisible
}

characteristic SSC_MATERIAL_ID {name "Material Number" textLength 18 reference table 'MARA' field 'MATNR'}
characteristic SSC_INSTANCE_NUMBER {name "Instance Number" numericLength 6}
characteristic SSC_DISPLAY_NAME {name "UI Display name"    textLength 50}
characteristic SSC_ITEM_QUANTITY_ACTUAL  {names EN 'Item quantity actual', DE 'Komponentenmenge' numericLength 13 decimalPlaces 3 negativeValues}
characteristic IS_PART_OF_SD_HARD {name "Is SD Component of (Hard):" classType SSC_PARENT }
characteristic IS_PART_OF_SD_SOFT {name "Is SD Component of (Soft):" classType SSC_PARENT }
characteristic IS_PART_OF_PP {name "Is PP Component of:" classType SSC_PARENT}
characteristic BOM_POSITION_NUMBER {name "Part_of item number" textLength 4 reference table 'CUXE1CUPRT' field 'PART_OF_NO'}
characteristic BOM_POSITION_CLASS_TYPE {name "Class Type" textLength 3 reference table 'CUXE1CUPRT' field 'CLASS_TYPE'}
characteristic BOM_POSITION_OBJECT_KEY {name "Object key" textLength 30 reference table 'CUXE1CUPRT' field 'OBJ_KEY'}
characteristic BOM_POSITION_OBJECT_TYPE {name "Object type" textLength 10 reference table 'CUXE1CUPRT' field 'OBJ_TYPE'}
characteristic BOM_POSITION_QUANTITY {names EN 'Component quantity', DE 'Komponentenmenge' numericLength 13 decimalPlaces 3 negativeValues reference table 'STPO' field 'MENGE'}
characteristic BOM_POSITION_QUANTITY_ORDERED {names EN "Actually Specified Conmponent Qty" numericLength 13 decimalPlaces 3 negativeValues}
characteristic BOM_POSITION_QUANTITY_NUMCHAR {names EN 'Component Quantity Numchar' textLength 6}
characteristic BOM_POSITION_QUANTITY_DISPLAY {names EN 'Component Quantity for display' numericLength 6}
characteristic BOM_POSITION_QUANTITY_IS_FIXED {names EN "Is fixed quantity BOM item" textLength 3 values 'YES','NO' }
characteristic HAS_PART_SD_HARD {name "Has SD Component (Hard):" classType SSC_CHILD multiValue}
characteristic HAS_PART_SD_SOFT {name "Has SD Component (Soft):" classType SSC_CHILD multiValue}
characteristic HAS_PART_PP {name "Has PP Component:" classType SSC_CHILD multiValue}
characteristic BOM_POSITION_DETAILS_MV {names EN "BOM Position Details" textLength 30 multiValue}
characteristic BOM_FIXED_QTY_DETAILS_MV {names EN "BOM Position Fixed Qty Details" textLength 30 multiValue}
characteristic SSC_CLASS {name "SSC Class" textLength 18}
characteristic ECC_KB_NAME {name "Knowledge-Base Object" textLength 30 reference table 'SCEKB'  field 'KBOBJNAME'}
characteristic ECC_KB_VERSION {name "Runtime Version of SCE Knowled" textLength 30 reference table 'SCEKB' field 'VERSION'}
characteristic NON_CONFIGURABLE_TYPES_IN_ECC {names EN "Non-configurable Types in ECC" textLength 30 multiValue}

constraintNet SSC_CONSTRAINTS {
	name "SSC-specific Constraints"
	constraints
	SET_HAS_PART_SD_SOFT,
	SET_HAS_PART_SD_HARD,
	SET_HAS_PART_PP,
	SET_BOM_POSITION_DETAILS,
	SET_ITEM_ACTUAL_QTY_FROM_HARD,
	SET_ITEM_ACTUAL_QTY_FROM_SOFT,
	SET_ITEM_ACTUAL_QTY_FROM_PP,
	SET_SSC_BOM_POS_QTY,
	SET_SSC_BOM_POS_QTY_DISPLAY,
	SET_SSC_BOM_POS_QTY_FIX_HARD,
	SET_SSC_BOM_POS_QTY_FIX_SOFT,
	SET_SSC_BOM_POS_QTY_FIX_PP,
	ROLLUP_BOM_FIXED_QTY_DTLS_HARD,
	ROLLUP_BOM_FIXED_QTY_DTLS_SOFT,
	ROLLUP_BOM_FIXED_QTY_DTLS_PP
}

constraint SET_HAS_PART_SD_SOFT {
	objects: ?SSC_CHILD is_a (300) SSC_CHILD where ?SSC_PARENT = IS_PART_OF_SD_SOFT
	restrictions:
	?SSC_PARENT.HAS_PART_SD_SOFT = ?SSC_CHILD
	inferences:
	?SSC_PARENT.HAS_PART_SD_SOFT
}

constraint SET_HAS_PART_SD_HARD {
	objects: ?SSC_CHILD is_a (300) SSC_CHILD where ?SSC_PARENT = IS_PART_OF_SD_HARD
	restrictions:
	?SSC_PARENT.HAS_PART_SD_HARD = ?SSC_CHILD
	inferences:
	?SSC_PARENT.HAS_PART_SD_HARD
}

constraint SET_HAS_PART_PP {
	objects: ?SSC_CHILD is_a (300) SSC_CHILD where ?SSC_PARENT = IS_PART_OF_PP
	restrictions:
	?SSC_PARENT.HAS_PART_PP = ?SSC_CHILD
	inferences:
	?SSC_PARENT.HAS_PART_PP
}

constraint SET_BOM_POSITION_DETAILS_REQ {names EN "Set BOM Position Details Required"
	status blocked
	objects: 
	?SSC_CHILD is_a (300) SSC_CHILD
	condition:
	?SSC_CHILD.IS_PART_OF_SD_SOFT specified or ?SSC_CHILD.IS_PART_OF_SD_HARD specified or ?SSC_CHILD.IS_PART_OF_PP specified
	restrictions:
	?SSC_CHILD.required BOM_POSITION_NUMBER,
	?SSC_CHILD.required BOM_POSITION_CLASS_TYPE,
	?SSC_CHILD.required BOM_POSITION_OBJECT_KEY,
	?SSC_CHILD.required BOM_POSITION_OBJECT_TYPE
	inferences:
	?SSC_CHILD.required BOM_POSITION_NUMBER,
	?SSC_CHILD.required BOM_POSITION_CLASS_TYPE,
	?SSC_CHILD.required BOM_POSITION_OBJECT_KEY,
	?SSC_CHILD.required BOM_POSITION_OBJECT_TYPE	
}

constraint SET_BOM_POSITION_DETAILS {names EN "Set BOM Position Details"
    objects:
    ?SSC_CHILD is_a (300)SSC_CHILD where ?SSC_PARENT = IS_PART_OF_SD_HARD
    restrictions:
    ?SSC_PARENT.BOM_POSITION_DETAILS_MV = ?SSC_CHILD.BOM_POSITION_NUMBER || '|' || ?SSC_CHILD.SSC_MATERIAL_ID || '|' || ?SSC_CHILD.BOM_POSITION_QUANTITY_NUMCHAR
}

constraint SET_ITEM_ACTUAL_QTY_FROM_HARD {
	objects:
	?CHILD is_a (300)SSC_CHILD where ?PARENT = IS_PART_OF_SD_HARD
	restrictions
	?CHILD.SSC_ITEM_QUANTITY_ACTUAL = ?PARENT.SSC_ITEM_QUANTITY_ACTUAL * ?CHILD.BOM_POSITION_QUANTITY
}

constraint SET_ITEM_ACTUAL_QTY_FROM_SOFT {
	objects:
	?CHILD is_a (300)SSC_CHILD where ?PARENT = IS_PART_OF_SD_SOFT
	restrictions
	?CHILD.SSC_ITEM_QUANTITY_ACTUAL = ?PARENT.SSC_ITEM_QUANTITY_ACTUAL * ?CHILD.BOM_POSITION_QUANTITY
}

constraint SET_ITEM_ACTUAL_QTY_FROM_PP {
	objects:
	?CHILD is_a (300)SSC_CHILD where ?PARENT = IS_PART_OF_PP
	restrictions
	?CHILD.SSC_ITEM_QUANTITY_ACTUAL = ?PARENT.SSC_ITEM_QUANTITY_ACTUAL * ?CHILD.BOM_POSITION_QUANTITY
}

constraint SET_SSC_BOM_POS_QTY {names EN "Set SSC Details"
	objects: 
	?CHILD is_a (300)SSC_CHILD
	restrictions:
	?CHILD.BOM_POSITION_QUANTITY = ?CHILD.BOM_POSITION_QUANTITY_ORDERED
}

constraint SET_SSC_BOM_POS_QTY_DISPLAY {names EN "Set BOM Position Qty Display"
	objects: 
	?CHILD is_a (300)SSC_CHILD
	restrictions:
	?CHILD.BOM_POSITION_QUANTITY_DISPLAY = ?CHILD.BOM_POSITION_QUANTITY_ORDERED
}

constraint SET_SSC_BOM_POS_QTY_FIX_HARD {names EN "Set SSC Details"
	objects: 
	?CHILD is_a(300)SSC_CHILD where ?PARENT = IS_PART_OF_SD_HARD
	condition: 
	?CHILD.BOM_POSITION_QUANTITY_IS_FIXED = 'YES'
	restrictions:
	?PARENT.BOM_FIXED_QTY_DETAILS_MV = ?PARENT.SSC_MATERIAL_ID || '|' || ?CHILD.BOM_POSITION_NUMBER
}

constraint SET_SSC_BOM_POS_QTY_FIX_SOFT {names EN "Set SSC Details"
	objects: 
	?CHILD is_a(300)SSC_CHILD where ?PARENT = IS_PART_OF_SD_SOFT
	condition: 
	?CHILD.BOM_POSITION_QUANTITY_IS_FIXED = 'YES'
	restrictions:
	?PARENT.BOM_FIXED_QTY_DETAILS_MV = ?PARENT.SSC_MATERIAL_ID || '|' || ?CHILD.BOM_POSITION_NUMBER
}

constraint SET_SSC_BOM_POS_QTY_FIX_PP {names EN "Set SSC Details"
	objects: 
	?CHILD is_a(300)SSC_CHILD where ?PARENT = IS_PART_OF_PP
	condition: 
	?CHILD.BOM_POSITION_QUANTITY_IS_FIXED = 'YES'
	restrictions:
	?PARENT.BOM_FIXED_QTY_DETAILS_MV = ?PARENT.SSC_MATERIAL_ID || '|' || ?CHILD.BOM_POSITION_NUMBER
}

constraint ROLLUP_BOM_FIXED_QTY_DTLS_HARD {names EN "Roll-up BOM Fixed Qty Details Hard"
	objects: 
	?CHILD is_a(300)SSC_CHILD where ?PARENT = IS_PART_OF_SD_HARD
	restrictions:
	?PARENT.BOM_FIXED_QTY_DETAILS_MV = ?CHILD.BOM_FIXED_QTY_DETAILS_MV			
}

constraint ROLLUP_BOM_FIXED_QTY_DTLS_SOFT {names EN "Roll-up BOM Fixed Qty Details Soft"
	objects: 
	?CHILD is_a(300)SSC_CHILD where ?PARENT = IS_PART_OF_SD_SOFT
	restrictions:
	?PARENT.BOM_FIXED_QTY_DETAILS_MV = ?CHILD.BOM_FIXED_QTY_DETAILS_MV			
}

constraint ROLLUP_BOM_FIXED_QTY_DTLS_PP {names EN "Roll-up BOM Fixed Qty Details PP"
	objects: 
	?CHILD is_a(300)SSC_CHILD where ?PARENT = IS_PART_OF_PP
	restrictions:
	?PARENT.BOM_FIXED_QTY_DETAILS_MV = ?CHILD.BOM_FIXED_QTY_DETAILS_MV			
}

ruleNet SSC_RULES {names EN "SSC Rules"
	rules
	 SET_ROOT_ACTUAL_ITEM_QTY
	,CONVERT_BOM_QTY_2_STRING
}

rule SET_ROOT_ACTUAL_ITEM_QTY {
	objects:
	?PARENT is_a(300)SSC_PARENT,
	?ROOT is_a(300)SSC_KB_ROOT
	condition
	?PARENT = ?ROOT
	body:
	then do: ?PARENT.SSC_ITEM_QUANTITY_ACTUAL ?= 1
}

rule CONVERT_BOM_QTY_2_STRING {names EN " Convert BOM Quantity 2 String"
  	objects:
	?CHILD is_a (300) SSC_CHILD
	condition: 
	?CHILD.BOM_POSITION_QUANTITY > 0.0
	body:
	then do:
	pfunction CONVERT_NUMERIC_2_NUMC (  
	BOM_POSITION_QUANTITY = ?CHILD.BOM_POSITION_QUANTITY_DISPLAY,
	BOM_POSITION_QUANTITY_NUMCHAR = ?CHILD.BOM_POSITION_QUANTITY_NUMCHAR
	)
}